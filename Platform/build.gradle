dependencies {
  compile 'com.l2fprod.common:l2fprod-common-directorychooser:0.2.0'
  compile 'commons-io:commons-io:2.1'
  compile 'org.reflections:reflections:0.9.8'
  compile 'com.itextpdf:itextpdf:5.2.0'
  compile 'com.google.code.gson:gson:2.1'
  compile 'de.idos.updates:updates-core:1.1.0'
  compile project(':Lib_Workflow')
  compile project(':Lib_Gui')
  testCompile parent.ext.testDependencies
}

configurations.all {
  resolutionStrategy {
    //This module, update-core and reflections all use commons-io.
    force 'commons-io:commons-io:2.1'
  }
}

processResources {
    String versionDate = new Date().format('MM/dd/yy')
    String versionString = project.parent.ext.versionString
    String versionName = version_name
    // work-around for filter corrupting binary resources
    from("./resources") {
        include '**/*.properties'
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [VersionString: versionString, VersionName: versionName, VersionDate: versionDate])
    }
    from("./resources") {
        exclude '**/*.properties'
    }
}

configurations {
  tests
}

artifacts {
  tests testJar
}

import org.apache.tools.ant.filters.ReplaceTokens 

def appName = "Anathema"
def appDir = "${appName}.app"
def dmgName = "${appName}_v${version_major}.${version_minor}.${version_revision}.dmg"
def mountdir = "/tmp"

def executeOnMac = {Closure command ->
  def os = System.getProperty("os.name").toLowerCase()
  if (os.contains("mac os")) {
    command.call()
  }
  else {
    throw new GradleException("This only works on Mac OS X.")
  }
}

task delete(type: Delete) {
  delete "$buildDir/$appDir"
}

task prepare << {
  new File("${buildDir}/${appDir}/Contents/MacOS").mkdirs()
  new File("${buildDir}/${appDir}/Contents/Resources/Java").mkdirs()
}

task createSymlink << {
  ['ln', '-s', "/System/Library/Frameworks/JavaVM.framework/Versions/Current/Resources/MacOS/JavaApplicationStub", "${buildDir}/${appDir}/Contents/MacOS/JavaApplicationStub"].execute().waitFor()
}

task copyContent(type: Copy) {
  destinationDir buildDir
  into("${appDir}/Contents/") {
    from("Development_Distribution/Mac/")
    include 'Info.plist'
    //filter(ReplaceTokens, tokens: ["version-major": "${version_major}", "version-minor": "${version_minor}", "version-revision": "${version_revision}"])
  }
  into("${appDir}/Contents/Resources") {
    from("Development_Distribution/Mac/")
    include 'sungear.icns'
  }
  into("${appDir}/Contents/Resources/Java") {
    from 'Anathema/build/libs'
  }
  into("${appDir}/Contents/Resources/Java") {
    from("Development_Documentation/Distribution/");
    include '**/*.txt'
  }
  into("${appDir}/Contents/Resources/Java/lib") {
    from("${buildDir}/dependencies/");
  }
  into("${appDir}/Contents/Resources/Java/plugins") {
    from("${buildDir}/plugins/");
  }
}

task buildMacApplication << {
}

prepare.dependsOn delete
[createSymlink, copyContent].each{
	it.dependsOn prepare
}
buildMacApplication.dependsOn prepare, createSymlink, copyContent